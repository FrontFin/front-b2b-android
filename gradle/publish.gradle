apply from: "$rootDir/gradle/secrets.gradle"
// See https://docs.gradle.org/current/userguide/publishing_maven.html
// and https://developer.android.com/studio/publish-library
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.getfront'
                artifactId = project.name
                version = publishVersion

                from components.release

                pom {
                    name = 'Front Finance Android'
                    description = 'Android library for integrating with Front Finance'
                    url = 'https://github.com/FrontFin/front-b2b-android'

                    scm {
                        url = 'https://github.com/FrontFin/front-b2b-android'
                        connection = 'scm:git:git://github.com/FrontFin/front-b2b-android.git'
                        developerConnection = 'scm:git:ssh://git@github.com/FrontFin/front-b2b-android.git'
                        tag = commitHash
                    }
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'frontfin'
                            name = 'Front Finance, Inc'
                            url = 'https://github.com/FrontFin'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = isReleaseVersion()
                        ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                        : 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

// See https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    required { isReleaseVersion() && gradle.taskGraph.hasTask('publish') }
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

static def getCommitHash() {
    return 'git rev-parse --verify HEAD'.execute().text.trim()
}

def isReleaseVersion() {
    return publishVersion != null && !publishVersion.endsWith("SNAPSHOT")
}
